using sys.core.Point2D;

namespace sys.core;

class Point3D<T: Class> {
	val X: T;
	val Y: T;
	val Z: T;

	this(value: T) {
		X = value;
		Y = value;
		Z = value;
	}

	this(x: T, y: T, z: T) {
		X = x;
		Y = y;
		Z = z;
	}

	this(p2d: Point2D<T>, z: T) {
		X = p2d.X;
		Y = p2d.Y;
		Z = z;
	}

	def Clamp(min: Point3D<T>, max: Point3D<T>) {
		X.Clamp(min.X, max.X);
		Y.Clamp(min.Y, max.Y);
		Z.Clamp(min.Z, max.Z);
	}

	def Clamp(min: T, max: T) {
		X.Clamp(min, max);
		Y.Clamp(min, max);
		Z.Clamp(min, max);
	}
	
	def Clamped(min: Point3D<T>, max: Point3D<T>): Point3D<T> {
		val c: Point3D<T> = void;

		c.X = X.Clamped(min.X, max.X);
		c.Y = Y.Clamped(min.Y, max.Y);
		c.Z = Z.Clamped(min.Z, max.Z);

		return c;
	}
	
	def Clamped(min: T, max: T): Point2D<T> {
		val c: Point3D<T> = void;

		c.X = X.Clamped(min, max);
		c.Y = Y.Clamped(min, max);
		c.Z = Z.Clamped(min, max);

		return c;
	}
	
	def GetMin(min: Point3D<T>): Point3D<T> {
		val c: Point3D<T> = void;

		c.X = X.GetMin(min.X);
		c.Y = Y.GetMin(min.Y);
		c.Z = Z.GetMin(min.Z);

		return c;
	}

	def GetMax(max: Point3D<T>): Point3D<T> {
		val c: Point3D<T> = void;

		c.X = X.GetMax(max.X);
		c.Y = Y.GetMax(max.Y);
		c.Z = Z.GetMax(max.Z);

		return c;
	}

	def IsNormalized(tolerance: T): Bool {
		return ((X * X) + (Y * Y) + (Z * Z) - T{1}).Abs < tolerance;
	}

	property Length: T
	get {
		return ((X * X) + (Y * Y) + (Z * Z)).Sqrt;
	}

	property LengthSquared: T
	get {
		return (X * X) + (Y * Y) + (Z * Z);
	}

	def Normalize(tolerance: T) {
		val length = Length;
		if (length > tolerance) {
			val inv = T{1} / length;
			X *= inv;
			Y *= inv;
			Z *= inv;
		}
	}

	def Normalized(tolerance: T): Point3D<T> {
		val t = this;
		val length = Length;
		if (length > tolerance) {
			val inv = T{1} / length;
			t.X *= inv;
			t.Y *= inv;
			t.Z *= inv;
		}
		return t;
	}

#region op

	def @add(second: Point3D<T>): Point3D<T> {
		return Point3D<T>{X + second.X, Y + second.Y, Z + second.Z};
	}

	def @add(second: T): Point3D<T> {
		return Point3D<T>{X + second, Y + second, Z + second};
	}

	static def @sub(left: Point3D<T>, right: Point3D<T>): Point3D<T> {
		return Point3D<T>{left.X - right.X, left.Y - right.Y, left.Z - right.Z};
	}

	static def @sub(left: Point3D<T>, right: T): Point3D<T> {
		return Point3D<T>{left.X - right, left.Y - right, left.Z - right};
	}

	static def @sub(left: T, right: Point3D<T>): Point3D<T> {
		return Point3D<T>{left - right.X, left - right.Y, left - right.Z};
	}

	def @mul(second: Point3D<T>): Point3D<T> {
		return Point3D<T>{X * second.X, Y * second.Y, Z * second.Z};
	}

	def @mul(second: T): Point3D<T> {
		return Point3D<T>{X * second, Y * second, Z * second};
	}

	static def @div(left: Point3D<T>, right: Point3D<T>): Point3D<T> {
		return Point3D<T>{left.X / right.X, left.Y / right.Y, left.Z / right.Z};
	}

	static def @div(left: Point3D<T>, right: T): Point3D<T> {
		return Point3D<T>{left.X / right, left.Y / right, left.Z / right};
	}

	static def @div(left: T, right: Point3D<T>): Point3D<T> {
		return Point3D<T>{left / right.X, left / right.Y, left / right.Z};
	}

	static def @mod(left: Point3D<T>, right: Point3D<T>): Point3D<T> {
		return Point3D<T>{left.X % right.X, left.Y % right.Y, left.Z % right.Z};
	}

	static def @mod(left: Point3D<T>, right: T): Point3D<T> {
		return Point3D<T>{left.X % right, left.Y % right, left.Z % right};
	}

	static def @mod(left: T, right: Point3D<T>): Point3D<T> {
		return Point3D<T>{left % right.X, left % right.Y, left % right.Z};
	}

	def @minus(): Point3D<T> {
		return Point3D<T>{-X, -Y, -Z};
	}
	
	def @eq(second: T): Bool {
		return X == second && Y == second && Z == second;
	}

	def @neq(second: T): Bool {
		return X != second || Y != second || Z != second;
	}

#endregion

	def Equals(second: Point3D<T>, tolerance: T): Bool {
		return (X - second.X).Abs < tolerance && (Y - second.Y).Abs < tolerance  && (Z - second.Z).Abs < tolerance;
	}

	def Equals(second: T, tolerance: T): Bool {
		return (X - second).Abs < tolerance && (Y - second).Abs < tolerance && (Z - second).Abs < tolerance;
	}
}

alias Point3Di = Point3D<Int>;
alias Point3Du = Point3D<DWord>;
alias Point3Df = Point3D<Float>;
alias Point3Dd = Point3D<Double>;