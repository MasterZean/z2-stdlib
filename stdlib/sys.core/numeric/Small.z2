using sys.core.ConvertUtf8;
using sys.core.Stream;
using sys.core.OutputFormat;

namespace sys.core.lang;

class Small {
	const Zero: Small = 0;
	const One: Small = 1;

	const Min: Small = -128;
	const Max: Small = 127;

	const IsSigned = true;
	const IsInteger = true;

	const MaxDigitsLow = 2;
	const MaxDigitsHigh = 3;

	property Abs: Small {
		return this > 0 ? this : -this;
	}

	property Sqr: Small {
		return this * this;
	}

	property Sqrt: Small {
		return Small{Double{this}.Sqrt};
	}

	property Floor: Small {
		return this;
	}

	property Ceil: Small {
		return this;
	}

	property Round: Small {
		return this;
	}

	def Clamp(min: Small, max: Small); Intrinsic.Clamp;
	
	def Clamped(min: Small, max: Small): Small; const Intrinsic.Clamped;

#region ToString

	def ToString(): String {
		val buf: [c ConvertUtf8.MaxSmallBuffer]Byte = void;
		val s = Slice<Byte>{buf};
		ConvertUtf8.IntAsText(s, this);
		return String{s};
	}

#endregion

#region Saturation

	this Saturated(value: Int) {
		if (value > Max)
			this = Max;
		else if (value < Min)
			this = Min;
		else
			this = Small{value};
	}

	this Saturated(value: DWord) {
		this = value > DWord{Max} ? Max : Small{value};
	}

	this Saturated(value: Long) {
		if (value > Max)
			this = Max;
		else if (value < Min)
			this = Min;
		else
			this = Small{value};
	}

	this Saturated(value: QWord) {
		this = value > QWord{Max} ? Max : Small{value};
	}

	this Saturated(value: Double) {
		if (value > Max)
			this = Max;
		else if (value < Min)
			this = Min;
		else
			this = Small{value};
	}

	this Saturated(const string: String) {
		val s = Slice<Byte>{string};
		this.Saturated{ConvertUtf8.ParseSigSaturated<Int>(s)};
	}
	
#endregion

	this(const string: String) {
		val s = Slice<Byte>{string};
		this = Small{ConvertUtf8.ParseSig<Int>(s)};
	}

#region IO

	def @write(ref stream: Stream); const {
		stream.RawWrite(this);
	}

	def @write(ref stream: Stream, const format: OutputFormat); const {
		stream.RawWrite(this, format);
	}
	
	def @put(ref stream: Stream); const {
		stream.RawPutS8(this);
	}

	def @get(ref stream: Stream) {
		this = stream.RawGetS8();
	}

#endregion
}