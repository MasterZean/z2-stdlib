namespace sys.core;

class ConvertUtf16 {
	static def FromUtf8(ref dest: Slice<Word>, ref src: Slice<Byte>) {
		val s = src.SysDataPointer;
		val l = s + src.Length;
		val d = dest.SysDataPointer;
		
		while (s < l) {
			val code: Word = s*;
			s++;
			
			if(code < 0x80)
				d* = code;
			else if(code < 0xE0) {
				d* = Word{((code - 0xC0) << 6) + (s[0] - 0x80)};
				s++;
			}
			else if(code < 0xF0) {
				d* = Word{((code - 0xE0) << 12) + ((s[0] - 0x80) << 6) + (s[1] - 0x80)};
				s += 2;
			}
			else if(code < 0xF8) {
				val cp = DWord{((code - 0xF0) << 18) + ((s[0] - 0x80) << 12) + ((s[1] - 0x80) << 6) + (s[2] - 0x80)};
				cp -= 0x10000;
				d* = Word{0xD800 + (0x3ff & (cp >> 10))};
				d++;
				d* = Word{0xDC00 + (0x3ff & cp)};
				s += 3;
			}
			
			d++;
		}
		
		dest.Length = d - dest.SysDataPointer;
	}
}